interface halide_hexagon_remote
{
    typedef sequence<octet> buffer;
    typedef unsigned long handle_t;

    // Routine to initialize a module on the remote side. Returns a pointer to the module.
    long initialize_kernels_v3(in buffer code, rout handle_t module_ptr);

    // Get a symbol from the module.
    // v2: return the result as an output parameter instead of a return value.
    long get_symbol_v4(in handle_t module_ptr, in sequence<char> name, rout handle_t sym_ptr);

    // If running many short-running pipelines will be executed, the
    // penalty for powering on HVX can be avoided by doing this
    // explicitly around a collection of pipeline invocations.
    long power_hvx_on();
    long power_hvx_off();

    // Routine to run a pipeline on the remote side.
    // v2: Pass all scalars and small input buffers in a
    // small_input_args buffer. The small_input_args buffer is packed
    // using the protocol in pack_buffer.h. It contains the input
    // buffers first, followed by the scalars. An input buffer is
    // passed in the small_input_args buffer if the input buffer is
    // null.
    long run_v2(in handle_t module_ptr, in handle_t symbol,
                in sequence<buffer> input_buffers,
                rout sequence<buffer> output_buffers,
                in long scalar_count, in buffer small_input_args);

    // Routine to clean up a module on the remote side.
    long release_kernels_v2(in handle_t module_ptr);

    // Retrive messages written by log_printf.
    long poll_log(rout sequence<char> log, rout long read_size);

    // Retrieve the current profiling Func ID
    long poll_profiler_state(rout long func, rout long threads);

    // Set a performance target.
    long set_performance_mode(in long mode);
    long set_performance(in long set_mips,
                         in unsigned long mipsPerThread,
                         in unsigned long mipsTotal,
                         in long set_bus_bw,
                         in unsigned long bwMegabytesPerSec,
                         in unsigned long busbwUsagePercentage,
                         in long set_latency,
                         in long latency);

};
